#AUTO
TLA=9.5;TKA=9.8;TM 1000
Rev=9.999;Version=3.0;CylNum=1;Frstim=0
RampA=0;StT2A=0;OvrPresA=0;RstPosA=0;
MO;CE 2;CN 1;MT -1;KPvel=4;KDvel=30;KIvel=0;FlowRA=0;FloVolA=0
KP 4;KD 30;KI 0;OverLim=0;ZCA=0;CONTA=0;lpD=50
PmodeA=0;DecrK=1;Occur=0;Tchng=0;PSetPt=0;DelaydA=0
AC 1000000;DC 2048000;AG 1;SP 1000000
SH;FState=0;Pmode=0;JogDir=0;ChngDir=0;JogDirL=0;Wait4op=0
PState=0;XdcrCur=1;jststrd=1;Fjststrd=1;Pready=0;ReqStp=0;StCylA=0
StReflA=0;Paused=0;TlastA=TIME;LstCntsA=_TPA;ErrMsg=0;SyncTru=0
InitDone=0;CyclDone=0;Transit=0;PTempA=0;NewErr=0;ResetMLA=0;Cycle=0
SPlow=0;SPhigh=0;FracLo=0;FracHi=0;LowFlow=0;CompErr=0;Finding=0
ReStpP=0;RateChng=0;PowerUp=1;Local=0;Remote=0
DM AveSpAr[10];AveSpTm=TIME;Speedi=0;Debug=0;AveSp=0
WT10000;JS#STRTDAQ;JS#ZEROOF
EN
#SPEED
  AveSpAr[9]=AveSpAr[8]
  AveSpAr[8]=AveSpAr[7]
  AveSpAr[7]=AveSpAr[6]
  AveSpAr[6]=AveSpAr[5]
  AveSpAr[5]=AveSpAr[4]
  AveSpAr[4]=AveSpAr[3]
  AveSpAr[3]=AveSpAr[2]
  AveSpAr[2]=AveSpAr[1]
  AveSpAr[1]=AveSpAr[0]
IF Aflow>0
    AveSpAr[0]=SPcurA;Speedi=Speedi+1
ELSE 
  AveSpAr[0]=0
ENDIF
IF Speedi>3;JS#SPEED2;ENDIF
EN
#SPEED2
Speedi=0
TempSpTo=(AveSpAr[0]+AveSpAr[1]+AveSpAr[2]+AveSpAr[3]+AveSpAr[4]+AveSpAr[5])
AveSp=((TempSpTo+AveSpAr[6]+AveSpAr[7]+AveSpAr[8]+AveSpAr[9])/10)
EN
#ADJSPA
SP SPhigh
WT FracLo
SP SPlow
WT FracHi
JP#ADJSPA
EN
#CHKLMTA
WT 25
JP#CHKLMTA,(MLcurA>Amin)&(MLcurA<Amax)
IF MLcurA<=Amin
  StartMLA=PmpStart
ELSE
  StartMLA=RcvStart
ENDIF
Wait4op=1
JP#REFILLA
EN
#CHKMLA
#LOOP1
WT 25
JP#STPBOTH,MLtot>=TarVol
JS#CHGRATE,RateChng>0
JP#LOOP1,MLcurA>TrnsVol
HX4
Wait4op=1;JG 0
JP#REFILLA
EN;EN
#CHGRATE
RateChng=0
#LOOPCHG
JP#LOOPCHG,(StReflA=1)
JS#VCALCSA
PmpSpdA=(PmpRate*CntsPMLA)/60
JG PmpSpdA
IF LowFlow=1;HX4;JS#LOSPPA;XQ#ADJSPA,4;ENDIF
EN;EN
#CMDERR
'ZC 0
ST;AM;MO;LineNum=_ED;ErrCode=_TC;NewErr=1;ErrMsg=100+ErrCode;HX
EN
#CURPA
AccumA=0;IncA=0
#REPEATA;AccumA=AccumA+@AN[1];IncA=IncA+1;WT tdla
JP#REPEATA,IncA<AveNo
IF Pxdcr<5;PcurA=((AccumA/IncA)-PzeroA)/VppsiA[Pxdcr];ENDIF
IF Pxdcr >=5;PcurA=(((AccumA/IncA)-PzeroA)/PfsVA)*PRange[Pxdcr];ENDIF
IF XdcrCur=1;Pcur=PcurA;ENDIF
IF Pcur>PMax
  OvrPres=OvrPres+1
  IF OvrPres>2
    JS#OpnAll
    JS#ESTOP
    ErrMsg=12;NewErr=1;Errline=_ED
  ENDIF
ELSE
  OvrPres=0
ENDIF
EN;EN
#CURMLA
MLcurA=_TPA/CntsPMLA;TcurA=TIME;CurCntsA=_TPA
IF TcurA-TlastA<>0;SPcurA=((LstCntsA-CurCntsA)/(TcurA-TlastA))/SpdFacA;ENDIF
LstCntsA=CurCntsA;TlastA=TcurA;
IF ((FState<>3)&(PState<>3))
  IF Aflow= 1;MLDispA=MLzeroA-MLcurA;ENDIF
  MLtot=MLDispA+MLtotA+-MLofstA
  IF (Finding=0)&(Cycle=0)  
    IF (MLcurA<(.03*VolA))|(MLcurA>(.97*VolA))
      TLA=2.5;TKA=2.8
    ELSE
      TLA=9.5;TKA=9.8
    ENDIF
  ENDIF
ENDIF
EN
#CYCLEA
JS#OpnAin;JS#ClsAout
MLtotA=0;MLDispA=0;MLofstA=0;OEA=1
JP#SKIPF2,_LFA=0
JG JgSpd;BGA;AMA
#SKIPF2
IF InVopn=0;JS#ClsAin;JS#OpnAout;ENDIF
MLnewA=StartMLA;Rate=InitRate
JS#NEWPOSA
AM A;AStopd=0;StReflA=0;OEA=0
EN;EN
#CYCTOST
TLA=3.5;TKA=3.8
Cycle=1;AStopd=1
XQ#CYCLEA,2
#CTSLOOP
JP#CTSLOOP, (AStopd<>0)
JS#ClsAll
Cycle=0;CyclDone=1
#ZEROT
WT 1000
MLcurA=_TPA/CntsPMLA;MLzeroA=MLcurA;MLofstA=0;MLtotA=0;MLDispA=0
TLA=9.5;TKA=9.8
EN;EN;EN
#DAQLOOP
JS#CURMLA
JS#SPEED
'JS#CURPA
IF XdcrCur=1;Pcur=PcurA;ENDIF
IF XdcrCur=0;Pcur=0;ENDIF
WT 200
JP#DAQLOOP,DAQmode>0
EN
#PDAQ
JS#CURPA
WT200
JP#PDAQ,DAQmode>0
EN
#ESTOP
STA;HX1;HX2;HX3;HX4;HX5
STA;OF0;FState=0
AM;ZCA=0;PState=0
FlowingA=0
StReflA=0
IF _XQ1>0;HX1;ENDIF
EN
#FLOWA
FlowingA=1
'Calculate Speed
SPA=(FlowRA*CntsPMLA)/60
'Calculate Volume
PR FloVolA*CntsPMLA
BGA;AMA;FlowingA=0
EN
#FNDLMTS
TLA=2.5;TKA=2.8
JS#ZEROOF
Finding=1.0;AStopd=1;CyclDone=0;InitDone=0
JS#FNDA
Finding=0;FState=0;PowerUp=0;InitDone=1
JS#ClsAll
TLA=9.5;TKA=9.8
EN
#FNDA
JS#OpnAin;JS#ClsAout
SHA;DC 2048000;OEA=1
JP#SKIPFA,_LFA=0
JG JgSpd;BGA;AM A
#SKIPFA
IF InVopn=0;JS#ClsAin;JS#OpnAout;ENDIF
FLCntsA = _TPA - CntMarg
JG -JgSpd;BGA;AM A
RLCntsA=_TPA + CntMarg
DeltCtsA=FLCntsA-RLCntsA
StkeA=DeltCtsA/CntsPmm
VolA=DeltCtsA/CntsPMLA
PR CntMarg;BGA;AM A
DP 0;AStopd=0
MLtotA=0;MLDispA=0;MLofstA=0;OEA=0
EN;EN
#LIMSWI
IF (Finding=1)|(Cycle=1)
  IF (_LFA=0)|(_LRA=0);STA;ENDIF
ELSE
  IF (_LFA=0)|(_LRA=0);DC 100000000;STA;AMA;DC 2048000;ENDIF
ENDIF
RE
#LOSPPA
SPlow=_SPA;SPhigh=SPlow+2;FracLo=(@ABS[PmpSpdA]-_SPA)*1000;FracHi=2000-FracLo
EN
#LSLOOP
IF (_LFA=0)
  ZCA=0
  ReStpP=1
  IF (PmpMode=1)&((PState=4)|(PState=5))
      NewErr=1;Errline=_ED;ErrMsg=19
  ELSE
    StCylA=0;NewErr=1;Errline=_ED;ErrMsg=20;HX2
    JS#ClsAout;JS#OpnAin;JS#SETPST
  ENDIF
ENDIF
IF (_LRA=0)
  ZCA=0
  ReStpP=1
  IF (PmpMode=0)&((PState=4)|(PState=5))
    NewErr=1;Errline=_ED;ErrMsg=19
  ELSE
    StCylA=0;NewErr=1;Errline=_ED;ErrMsg=20;HX2
    JS#ClsAout;JS#OpnAin;JS#SETPST
  ENDIF
ENDIF
JP#LSLOOP
EN
#MASTPLP
JS#NEWSP,Ramping=1
IF (ReStpP=1)
  IF PmodeA>0  
    JS#SETPFBA
    SHA;HX2;HX5
  ENDIF
  Pmode=PmodeA
  IF (PState<>3) 
      PState=0
  ENDIF
  Pready=0
ENDIF
WT 200
JP#MASTPLP,PState>0
ReStpP=0
IF ZCA >0
  JS#SETPFBA
  HX2;HX5;SHA
ENDIF
JS#ZEROOF
HX3
EN
#MLSCALA
MLPmmA=(3.14159*RodDia[RodSize]*RodDia[RodSize])/4000
#CALCNTA
StkML=TotStke*MLPmmA
CntsPMLA=CntsPmm/MLPmmA
SpdFacA=(@RND[(CntsPMLA/10)]/6144)
CntMarg = Margin*CntsPmm
IF Burn>0;Burn=0;BV;ENDIF
EN;EN
#NEWPOSA
TarPosA=MLnewA*CntsPMLA
SP (Rate*CntsPMLA)/60
PA TarPosA;BGA;AM A
JS#CURMLA
FState=0
EN
#NEWSP
IF TarPres<PSetPt
  RampRate=-RampRate
ENDIF
Pinc=RampRate/10
IF Pinc=0
  Ramping=0
ENDIF
#RAMP
sttime=TIME
PSetPt=PSetPt + Pinc
IF Pinc>0
  IF PSetPt>=TarPres
    PSetPt=TarPres
    Ramping=0
  ENDIF
ELSE
  IF PSetPt<TarPres
    PSetPt=TarPres
    Ramping=0
  ENDIF
ENDIF
JS#SETSPV
WT 80
sptime=TIME
dtime=sptime-sttime
JP#RAMP,Ramping>0
IF SyncTru=1
  SyncTru=0
  Wait4op=3
#WAIT3
  JP#WAIT3,Wait4op>0  
  JS#OpnAout
  JP#CHKLMTA
ENDIF
EN;EN;EN
#PCNTRLA
JS#ClsAin
XdcrCur=1;PState=1;StCylA=3;ReStpP=0
'JS#SETSPV
JS#SETFFBO
JS#OpnAout
Pready=1
EN
#PLOOPA
IsumA=0
AT 0
#PLA
AT -lpD
'
VfiltA=(0.75*VlastA)+(0.25*@AN[1]);VlastA=VfiltA;XcurA=_TPA
VelcurA=(XcurA-XlastA)/lpD;XlastA=XcurA;errorA=VfiltA-SPvoltA
IF VelcurA>2000;VelcurA=2000;ENDIF;IF VelcurA<-2000;VelcurA=-2000;ENDIF
IsumA=IsumA+errorA;MotorVA=(KgainA*errorA)+(IgainA*IsumA)-(DgainA*VelcurA)
IF MotorVA>3;MotorVA=3;ENDIF;IF MotorVA<-3;MotorVA=-3;ENDIF
OF MotorVA
IF @ABS[@AN[1]]>4
  OF 0
  MO
  PmodeA=0
ENDIF
JP#PLA,PmodeA>0
OF 0;STA;AMA;StPctlA=0
EN;EN
#POSERR
IF PState=0
  NewErr=1;ErrMsg=11;ST;AM;MO;SH
ENDIF
RE
#REFILLA
JP#REFILLA,Wait4op>0
StReflA=1;StCylA=1
IF PState>0
  JS#SETPST
ELSE
  FState=3
ENDIF
JS#ClsAout
WT 500
JS#OpnAin
IF ZCA>0
  JS#SETPFBA
ENDIF
STA;AMA
MLtotA=MLtotA+MLDispA;MLDispA=0;Transit=0
SP RevSpdA;TarPosA =StartMLA*CntsPMLA
PA TarPosA;BGA;AMA
Wait4op=2
#WAIT2
JP#WAIT2,Wait4op>0
JS#ClsAin;MLcurA=_TPA/CntsPMLA;MLzeroA=MLcurA
IF CONTA=1
  CONTA=0
  StReflA=0
  FState=0
  JP#EXTRFA
ENDIF;'Stop here if called by STRTPRS
IF CompErr=1;ReStpP=1;ENDIF ;'We need to stop pressure loop after this refill
IF (ReStpP=1) | (ReqStp=1);'we will not continue
  StReflA=0;Fjststrd=0;FState=0;PState=0;StCylA=0
'
  JP#EXTRFA
ELSE
  IF PState>0
    StReflA=0
    JP#SYNCA
  ELSE
    FState=1
    IF (ReqStp=0)
      StReflA=0
      JP#STARTA
    ELSE
      StReflA=0
      Fjststrd=0
    ENDIF
  ENDIF
ENDIF
#EXTRFA
EN;EN;EN
'
#RCYCA
Cycle=1
JP#SKPRA,_LRA=0
JG -JgSpCal;BGA;AM A
#SKPRA
MLnewA=CalStrtA;Rate=RateCal
JS#NEWPOSA
AM A
WT 1000
MLcurA=_TPA/CntsPMLA;MLzeroA=MLcurA;MLofstA=0;MLtotA=0
Cycle=0
EN;EN
#RESET
RstPosA=ResetMLA*CntsPMLA
DP RstPosA
EN
#SAVEML
MLaccum=MLaccum+DeltaML;BV
EN
#SETFFBO
JS#SETKPA
OEA=0;PSetPt=PcurA;ZCA=1;PmodeA=1
JS#SETSPV
IF _XQ5<0;XlastA=_TPA;VlastA=@AN[1];XQ#PLOOPA,5;ENDIF
IF _XQ2<0;XQ#CHKLMTA,2;ENDIF
EN
#SETPFBA
ZCA=0;PmodeA=0;JS#SETKVA
EN
#SETKPA
MOA;KP 0;KD 0;KI 0;KgainA=KPP;DgainA=KDP;IgainA=KIP;SHA
IF _XQ3<0;XQ#LSLOOP,3;ENDIF
EN
#SETKVA
MOA;KP KPvel;KD KDvel;KI KIvel;KgainA=0;DgainA=0;IgainA=0;SHA
HX3
EN
#SETPST
IF  StCylA=3
    PState=1
ENDIF
IF StCylA=2
    PState=4
ENDIF
IF StCylA=1
    PState=3
ENDIF    
EN  
#SETSPV
IF Pxdcr<5
 SPvoltA=(VppsiA[Pxdcr]*PSetPt)+PzeroA
ENDIF
IF Pxdcr >=5
 SPvoltA=((PSetPt/PRange[Pxdcr])*PfsVA)+PzeroA
ENDIF
EN
#STARTA
FState=1;XdcrCur=1;ReqStp=0;ReStpP=0
JS#AACTIV
JS#VCALCSA
IF (_BGA=0)
  JG 0;BGA
ELSE
  JG 0
ENDIF
JG PmpSpdA;BGA;StCylA=3
IF LowFlow=1;JS#LOSPPA;XQ#ADJSPA,4;ENDIF
#FINA
'JP#FINA,_XQ2>0
JP#CHKMLA
EN;EN
#STPBOTH
ReqStp=1
IF FState=1;STA;ENDIF
AM;ZCA=0;IF _XQ4>0;HX4;ENDIF
StReflA=0;Fjststrd=0;FState=0;PState=0
StCylA=0;FState=0;HX2
EN
#STRTDAQ
WT 5
DAQmode=1;XQ#PDAQ,7;JP#DAQLOOP
EN
#STRTMPL
Pmode=1;PState=7;XQ#MASTPLP,4
EN
#STRTFLO
XQ#STARTA,2
EN
#STRTPOS
IF PmpMode=1;StartMLA=PmpStart;StartMLB=PmpStart;ENDIF
IF PmpMode=0;StartMLA=RcvStart;StartMLB=RcvStart;ENDIF
IF Burn>0;Burn=0;BV;ENDIF
EN
#STRTPRS
Pready=0
IF (StReflA=1);JP#STRTPRS;ENDIF
jststrd=1;ReflA=0;ReqStp=0;ReStpP=0
RevSpdA=(ReflPc*CntsPMLA)/60
IF RevSpdA>JgSpdMx;RevSpdA=JgSpdMx;ENDIF
IF RevSpdA<JgSpdMn;RevSpdA=JgSpdMn;ENDIF
IF (MLcurA>(Amax))|(MLcurA<(Amin));'Ais outside limits
    StReflA=1;CONTA=1
    Wait4op=1
    XQ#REFILLA,2;StCylA=1
ENDIF
#WAIT4RL
JP#WAIT4RL,_XQ2>0 
PSetPt=PcurA
JS#STRTMPL
JP#PCNTRLA
EN;EN
#STPCYCL
STA;Cycle=0;JS#ClsAll;HX1;HX2;HX4;HX6
EN
#STPDAQ
DAQmode=0
EN
#STPDP
ReStpP=1
EN
#SETSCAL
JS#MLSCALA
EN
#SYNCA
StCylA=2;PState=4; AMA
JS#ClsAout;JS#ClsAin
'Start Pressure control loop, control at current pressure
JS#SETFFBO
'Ramp up to current TarPres
SyncTru=1;Ramping=1;JP#NEWSP
EN
#TESTCAL
DM RodDia[10]
DM PRange[8];DM VppsiA[8];DM VppsiB[8]
RodDia[0]=6.35
RodDia[1]=9.525
RodDia[2]=11.1125
RodDia[3]=15.875
RodDia[4]=22.225
RodDia[5]=28.575
RodDia[6]=39.675
RodDia[7]=48.00
RodDia[8]=7.874
RodDia[9]=17.46
PRange[0]=20.0
PRange[1]=50.0
PRange[2]=125.0
PRange[3]=200.0
PRange[4]=500.0
PRange[5]=1000
PRange[6]=5000
PRange[7]=10000
EN
#VCALCSA
'TranStps=(Tdec/lpDlay)*1000
'DecVol=(-PmpRate/120)*Tdec
PmpSpdA=(PmpRate*CntsPMLA)/60
IF @ABS[PmpSpdA]<2000;LowFlow=1;ELSE;LowFlow=0;IF _XQ4>0;HX4;ENDIF;ENDIF
TrnsVol= StartMLA-VolpCyl+DecVol
IF TrnsVol <(DecVol+0.5);TrnsVol=DecVol+0.5;ENDIF
IF TrnsVol>(StartMLA+1.00);TrnsVol=(StartMLA+1.00);ENDIF
RevSpdA=-PmpSpdA*(VolpCyl/(.75*(VolpCyl-(2*DecVol))))
IF RevSpdA>(1.1*(FRMax*CntsPMLA)/60)
  RevSpdA=(1.1*(FRMax*CntsPMLA)/60)
ENDIF
IF RevSpdA>JgSpdMx;RevSpdA=JgSpdMx;ENDIF
IF RevSpdA<JgSpdMn;RevSpdA=JgSpdMn;ENDIF
EN
#VRECALA
JS#CALCNTA
BV
EN
#VSCALE
VppsiA[Pxdcr]=PfsVA/PRange[Pxdcr]
BV
EN
#ZEROML
JS#CURMLA,DAQmode=0
MLofstA=MLDispA;MLtotA=0
BV
EN
#ZEROVA
IF Pxdcr < 5
PzeroA=PzeroA + (PcurA*VppsiA[Pxdcr]);BV
ENDIF
IF Pxdcr >=5
PzeroA=PzeroA + (PcurA*PfsVA/PRange[Pxdcr]);BV
ENDIF
EN
#ZEROBTH
JS#ZEROVA
EN
#ZEROOF
OF0
EN
#OpnAout
SB 2;Aflow=1;EN
#ClsAout
CB 2;Aflow=0;EN
#OpnAin
SB 1;EN
#ClsAin
CB 1;EN
#OpnBout
SB 4;Bflow=1;EN
#ClsBout
CB 4;Bflow=0;EN
#OpnBin
SB 3;EN
#ClsBin
CB 3;EN
#OpnAll
JS#OpnAout;JS#OpnAin;JS#OpnBout;JS#OpnBin;EN
#ClsAll
JS#ClsAout;JS#ClsAin;JS#ClsBout;JS#ClsBin;EN
#OpnOut
JS#OpnAout;JS#OpnBout;EN
#OpnIn
JS#OpnAin;JS#OpnBin;EN
#AACTIV
JS#ClsBout;JS#ClsBin;JS#ClsAin;JS#OpnAout;EN
#BACTIV
JS#ClsAout;JS#ClsAin;JS#ClsBin;JS#OpnBout;EN
#ClsIn
JS#ClsAin;JS#ClsBin;EN
#ClsOut
JS#ClsAout;JS#ClsBout;EN
'
#DEFSPD
JgSpd=(InitRate*CntsPMLA)/60
JgSpdMn=(ReflFmn*CntsPMLA)/60
JgSpCal=(RateCal*CntsPMLA)/60
EN
#FACTSET
TestMode=0;DontOpn=0;CfgVer=7
PmpSpdA=0;MLzeroA=0;MLaccum=0;Kgain=0
Igain=0;Dgain=0;MLofstA=0
MLtotA=0;TarVol=2000000000;lpDlay=5;MLDispA=0
HiSpdA=0;DSpdA=0;LoSpdA=0;RevSpdA=0
MLlastA=0;Kx=1;MaxPerr=500;InVopn=1
MLcurA=0;MLtot=0;TrnsVol=0;PSetPt=0;SPlocal=0;
Tzero=0;RLCntsA=0;FLCntsA=0;DeltCtsA=0;AveNo=250;tdla=4;KP 4;KD 30;KI 0
Aflow=0;Bflow=0;PfiltA=0;PlastA=0;RampRate=0;TarPres=0
RLCntsB=0;FLCntsB=0;DeltCtsB=0;MLnewA=0;DeltaML=0;Ramping=0
OEA=0;ER 1000000;PzeroA=0;Burn=0;Pcur=0;PcurA=0
LstCntsA=0
JS#VESPRM
JS#MECHPRM
JS#TESTCAL
IF PRange[Pxdcr]>1000
  KPP=20;KDP=0.002;KIP=0.1
ELSE
  KPP=4;KDP=0.003;KIP=0.03
ENDIF
JS#SYSCFG
EN
#MECHPRM
RodSize=1;'Pxdcr=2
PfsVA=5.000
BSLead = 5
ECtsPR = 20000
'MotorRed = 25;'test vpa
'MotorRed = 50;'standard units
TotStke = 320
Margin = 5
EN
#SYSCFG
CntsPmm=(ECtsPR*MotorRed)/BSLead
JgSpdMx=1900000
JS#SETSCAL
JS#DEFSPD
JS#VSCALE
BV;BN
EN
#VESPRM
StkeA=320;VolA=20;StartMLA=18;PmpRate=-10
Amin=1.5;Amax=19.5;FwdAlrmA=20.5;PMax=10000
RevAlrmA=0.5;Tdec=2;VolpCyl=19.5;CalStrtA=18
ReflPc=10;StrtTol=2;TranStps=500;DecVol=3;FRMax=20;ErrVol=.25*VolA
InitRate=20;ReflFmn=1;PmpMode=1;PmpStart=18;RcvStart=2;RateCal=10
RefillRA=10
EN
